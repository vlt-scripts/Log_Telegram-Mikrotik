# INÍCIO DA CONFIGURAÇÃO | V4.0
:local bot "XXXXXXXXXXXXXXXXXXXXXX"
:local ChatID "XXXXXXXXXXXXXXXXXXX"
:local myserver ("%E2%9C%85 [" . [/system identity get name] . "] %E2%9C%85 %0A%0A %E2%99%A6 Alerta Logs:%E2%99%A6 %0A ")
:local scheduleName "LogFilter"
:local startBuf [:toarray [/log find where message~"authentication failed"]]
:local removeThese {"link";"telnet"}
# FIM DA CONFIGURAÇÃO

# Renomeia script1 para LogFilter se não existir um script com esse nome
:local newName "LogFilter"
:local existingScript [/system script find where name=$newName]
:if ([:len $existingScript] = 0) do={
    /system script set script1 name=$newName
    /log warning "Script renomeado para $newName"
}

# Converter Oct/10/2021 para data numérica: 10/10/2021 -> $mesnum
:global mesnum
:local date [/system clock get date]

# Avisar se o agendamento não existir e criá-lo
:if ([:len [/system scheduler find name="$scheduleName"]] = 0) do={
    /log warning "[LogFilter] Alerta: O agendamento não existe. Criando agendamento ...."
    /system scheduler add name=$scheduleName interval=60s start-date=May/01/2024 start-time=00:00:00 on-event=LogFilter
    /log warning "[LogFilter] Alerta: Agendamento criado!"
}

# Obter o último horário do comentário do agendador
:local lastTime [/system scheduler get [find name="$scheduleName"] comment]
:local currentTime
:local message
:local output
:local keepOutput false

# Se lastTime estiver vazio, definir keepOutput como verdadeiro
:if ([:len $lastTime] = 0) do={
    :set keepOutput true
}

:local counter 0

# Loop através de todas as entradas de log encontradas
:foreach i in=$startBuf do={
    :local keepLog true

    # Exceção para "authentication failed: peer didn't respond to CHAP challenge"
    :if ([/log get $i message] ~ "authentication failed: peer didn't respond to CHAP challenge") do={
        :set keepLog false
    } else={
        # Loop através de todos os itens do array removeThese
        :foreach j in=$removeThese do={
            # Se esta entrada de log contiver qualquer um deles, será ignorada
            :if ([/log get $i message] ~ "$j") do={
                :set keepLog false
            }
        }
    }

    # Se keepLog for verdadeiro, adicionar esta entrada de log à saída
    :if ($keepLog = true) do={
        :set message [/log get $i message]

        # Formatar data e hora do log
        :set currentTime [ /log get $i time ]
        # Formato de 00:00:00 para logs do dia atual
        :if ([:len $currentTime] = 8 ) do={
            :set currentTime ([:pick [/system clock get date] 0 11]." ".$currentTime)
        } else={
            # Formato de Jan/01 00:00:00 para logs do dia anterior
            :if ([:len $currentTime] = 15 ) do={
                :set currentTime ([:pick $currentTime 0 6]."/".[:pick [/system clock get date] 7 11]." ".[:pick $currentTime 7 15])
            }
        }

        # Se keepOutput for verdadeiro, adicionar esta entrada de log à saída
        :if ($keepOutput = true) do={
            :set output ($output. "%F0%9F%94%B6 ".$currentTime.$message."%0A%0A")
        }

        # Se currentTime for igual a lastTime, definir keepOutput como verdadeiro e limpar a saída
        :if ($currentTime = $lastTime) do={
            :set keepOutput true
            :set output ""
        }
    }

    # Incrementar o contador
    :if ($counter = ([:len $startBuf])-1) do={
        :if ($keepOutput = false) do={
            :if ([:len $message] > 0) do={
                :set output ($output.$currentTime."%0A%E2%99%A6 ".$message)
            }
        }
    }
    :set counter ($counter + 1)
}

# Se a saída tiver algum conteúdo, atualizar o comentário do agendador e enviar mensagem pelo Telegram
if ([:len $output] > 0) do={
    /system scheduler set [find name="$scheduleName"] comment=$currentTime
    /tool fetch url="https://api.telegram.org/bot$bot/sendMessage\?chat_id=$ChatID&text=$myserver%0A%0A$output" keep-result=no;
}
